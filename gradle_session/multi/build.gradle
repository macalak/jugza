//Enable default tasks for this project
defaultTasks 'build'

allprojects{
  ext.springVersion = '4.1.6.RELEASE'
  ext.junitVersion = '4.12'
  ext.slf4jVersion = '1.7.12'
  ext.log4jVersion = '1.2.17'
  ext.hsqldbVersion = '2.3.2'
  ext.mysqlVersion = '5.1.17'
  ext.hibernateVersion = '4.3.9.Final'
  ext.servletVersion = '3.1.0'
  ext.jstlVersion = '1.2'
  ext.hibernateJpaVersion = '1.0.0.Final'
  
  
  ext.libraries = [
    jsr330: 'javax.inject:javax.inject:1',
    logging: [
         "org.slf4j:slf4j-api:$slf4jVersion",
         "org.slf4j:slf4j-log4j12:$slf4jVersion",
         "log4j:log4j:$log4jVersion"
    ],
    hibernate: [
    	"org.hibernate:hibernate-core:$hibernateVersion",
    	"org.hibernate:hibernate-entitymanager:$hibernateVersion"
    ],
    jpa2:		  "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:$hibernateJpaVersion",	
    servlet:  "javax.servlet:javax.servlet-api:$servletVersion",
    jstl:     "javax.servlet:jstl:$jstlVersion", 
    junit: "junit:junit:$junitVersion",
    hsqldb: "org.hsqldb:hsqldb:$hsqldbVersion",
    mysql:  "mysql:mysql-connector-java:$mysqlVersion"
]

}


subprojects{
  apply plugin: 'java'
  apply plugin: 'eclipse-wtp'
  apply plugin: 'maven-publish' 
  
  group = 'ite.librarymaster'
  version = "$buildVersion.$buildNumber"  

  buildDir="build_gradle"

  repositories {
    jcenter()
    //maven { url "http://repo.springsource.org/release" }
  }

  test.doLast{println "test LAST..."}
 
  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  publishing {
    repositories {
        maven {
         credentials {
            username "$artifactory_publish_username"
            password "$artifactory_publish_password"
          }
          url "$artifactory_publish_url"
        }
         maven {
            url "$buildDir/repo"
        } 
    }
  } 
  

  dependencies {
    testCompile libraries.junit
  }
    
  task printDependencies (description: 'Where are jar files?') << {
	configurations.getByName('compile').each { dependency ->
		println dependency
	}
	
}
 
}

// Example of embedded Groovy code
task incrementBuildNumber << { 
  Properties props = new Properties() 
  File propsFile = new File('gradle.properties') 
  props.load(propsFile.newDataInputStream()) 
  Integer nextbuildnum = ( ((props.getProperty('buildNumber')) as BigDecimal) + 1 ) 
  props.setProperty('buildNumber', nextbuildnum.toString()) 
  props.store(propsFile.newWriter(), null) 
  props.load(propsFile.newDataInputStream()) 
} 

task generateWrapper(type: Wrapper) {
	gradleVersion = '2.11'
}

task mytask  {
	println "myTask CONFIG..."
	logger.info('Logger from mytask...')
	doFirst{
		println "myTask ACTION..."
		logger.info('Logger from mytask ACTION...')
	}
}

task printProperties(description: 'Prints properties.') << {
	println "$artifactory_publish_url"
}

task hello << {
    String greeting = 'hello from Ant'
    ant.echo(message: greeting)
}

// Dynamic tasks
4.times { counter ->
    task "dtask$counter" << {
        println "I'm dynamic task number $counter"
    }
}
dtask0.dependsOn dtask2, dtask3